#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/shadowcommon.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/PhaseFunctions.fxsub"

float mR : CONTROLOBJECT<string name="(self)"; string item = "R+";>;
float mG : CONTROLOBJECT<string name="(self)"; string item = "G+";>;
float mB : CONTROLOBJECT<string name="(self)"; string item = "B+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item = "Range+";>;
float mRangeM : CONTROLOBJECT<string name="(self)"; string item = "Range-";>;
float mIntensityP : CONTROLOBJECT<string name="(self)"; string item = "Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name="(self)"; string item = "Intensity-";>;
float mBlink : CONTROLOBJECT<string name="(self)"; string item = "Blink";>;
float mAttenuationBulbP : CONTROLOBJECT<string name = "(self)"; string item = "AttenuationBulb+";>;
float mAttenuationBulbM : CONTROLOBJECT<string name = "(self)"; string item = "AttenuationBulb-";>;
float mMiePhaseP : CONTROLOBJECT<string name="(self)"; string item = "MiePhase+";>;
float mMiePhaseM : CONTROLOBJECT<string name="(self)"; string item = "MiePhase-";>;
float mMieDensityP : CONTROLOBJECT<string name="(self)"; string item = "MieDensity+";>;
float mMieDensityM : CONTROLOBJECT<string name="(self)"; string item = "MieDensity-";>;

float3 mPosition : CONTROLOBJECT<string name="(self)"; string item="Position";>;
float3 mDirection : CONTROLOBJECT<string name="(self)"; string item="Direction";>;

static const float FogBlink = saturate(1 - SmoothTriangleWave(time * mBlink * 10));
static const float FogMiePhase  = lerp(lerp(FogMieParams.x, FogMieParams.z, mMiePhaseP), FogMieParams.y, mMiePhaseM);
static const float FogRange = lerp(lerp(FogRangeParams.x, FogRangeParams.z / 1.5, mRangeP), FogRangeParams.y, mRangeM);
static const float FogDensity = lerp(lerp(FogDensityParams.x, FogDensityParams.y, mMieDensityP), FogDensityParams.z, mMieDensityM);
static const float FogIntensity = lerp(lerp(FogIntensityParams.x, FogIntensityParams.z, mIntensityP), FogIntensityParams.y, mIntensityM);
static const float FogAttenuationBulb = lerp(lerp(FogAttenuationBulbParams.x, FogAttenuationBulbParams.z, mAttenuationBulbP), FogAttenuationBulbParams.y, mAttenuationBulbM);

static const float3 FogPosition = mPosition;
static const float3 FogDirection = normalize(mDirection - mPosition);
static const float3 FogColor = float3(mR, mG, mB) * FogIntensity * FogBlink;

#if VOLUMETRIC_FOG_ENABLE
#if VOLUMETRIC_FOG_QUALITY == 0
#	define VOLUMETRIC_FOG_WIDTH 256
#	define VOLUMETRIC_FOG_HEIGHT 512
#elif VOLUMETRIC_FOG_QUALITY == 1
#	define VOLUMETRIC_FOG_WIDTH 512
#	define VOLUMETRIC_FOG_HEIGHT 1024
#elif VOLUMETRIC_FOG_QUALITY >= 2
#	define VOLUMETRIC_FOG_WIDTH 1024
#	define VOLUMETRIC_FOG_HEIGHT 2048
#elif VOLUMETRIC_FOG_QUALITY >= 3
#	define VOLUMETRIC_FOG_WIDTH 2048
#	define VOLUMETRIC_FOG_HEIGHT 4096
#else
#	define VOLUMETRIC_FOG_WIDTH 256
#	define VOLUMETRIC_FOG_HEIGHT 512
#endif

texture VolumetricMap: OFFSCREENRENDERTARGET <
	string Description = "Volumetric fog Rendering for PointLightIES";
	int Width = VOLUMETRIC_FOG_WIDTH;
	int Height = VOLUMETRIC_FOG_HEIGHT;
	string Format = "R16F";
	float4 ClearColor = { 1, 1, 1, 1 };
	float ClearDepth = 1.0;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller.pmx=hide;"
		"*.pmx = ../ies_fog_shadow.fx;"
		"*.pmd = ../ies_fog_shadow.fx;"
		"* = hide";
>;
sampler VolumetricMapSamp = sampler_state {
	texture = <VolumetricMap>;
	MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};
#endif

texture IESTexture<string ResourceName = LIGHT_PARAMS_FILE; int Miplevels = 1;>; 
sampler IESTextureSamp = sampler_state
{
	texture = <IESTexture>;
	MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE;
	AddressU  = CLAMP; AddressV = CLAMP;
};

struct FogParams
{
	float3 FogPosition;
	float3 FogDirection;
	float3 FogColor;

	float FogRange;
	float FogDensity;
	float FogIntensity;
	float FogMiePhase;
	float FogAttenuationBulb;
};

float ComputeIESLightProfile1D(float3 L, float3 Ld)
{
	return tex2Dlod(IESTextureSamp, float4(acos(dot(L, Ld)) / PI, 0, 0, 0)).r;
}

float2 ComputeRaySphereIntersection(float3 position, float3 dir, float3 center, float radius)
{
	float3 origin = position - center;
	float B = dot(origin, dir);
	float C = dot(origin, origin) - radius * radius;
	float D = B * B - C;

	float2 minimaxIntersections;
	if (D < 0.0)
	{
		minimaxIntersections = -1;
	}
	else
	{
		D = sqrt(D);
		minimaxIntersections = float2(-B - D, -B + D);
	}

	return minimaxIntersections;
}

float ComputeOpticalDepth(FogParams param, float depth)
{
	return exp(-depth * param.FogDensity);
}

float GetPhysicalLightAttenuation(float3 L, float radius, float attenuationBulbSize)
{
	const float invRadius = 1 / radius;
	float d = length(L);
	float fadeoutFactor = saturate((radius - d) * (invRadius / 0.2h));
	d = max(d - attenuationBulbSize, 0);
	float denom = 1 + d / attenuationBulbSize;
	float attenuation = fadeoutFactor * fadeoutFactor / (denom * denom);
	return attenuation;
}

float HitShadowTest(FogParams param, sampler shadowMap, float shadowSize, float3 worldPosition)
{
	float3 L = worldPosition.xyz - param.FogPosition;

	float linearDepth = length(L);

	float depthScale = 85;
	float depth = (mPointLightFar / linearDepth) * (linearDepth - mPointLightNear) / (mPointLightFar - mPointLightNear);

	float2 coord = CalcDualShadowCoord(L / linearDepth, shadowSize);
	return tex2Dlod(shadowMap, float4(coord, 0, 0)).r < depth ? 0 : 1;
}

float4 VolumetricFog(FogParams param, float3 V, float3 start, float3 end, float jitter, int stepNum = VOLUMETRIC_FOG_SAMPLES_LENGTH)
{
	float3 sampleStep = (end - start) / stepNum;
	float3 sampleStart = start + sampleStep * jitter;

	float sampleLength = length(sampleStep);
	float sampleOpticalDepth = ComputeOpticalDepth(param, sampleLength);

	float scatteringMie = 0;
	float scatteringDepth = 1;

	for (int i = 0; i < stepNum; i++, sampleStart += sampleStep, scatteringDepth *= sampleOpticalDepth)
	{
		float density = sampleLength;
		float3 L = sampleStart - param.FogPosition;
		float3 Lnormalized = normalize(L);

#if VOLUMETRIC_FOG_ENABLE
		density *= HitShadowTest(param, VolumetricMapSamp, VOLUMETRIC_FOG_HEIGHT, sampleStart);
		density *= ComputeIESLightProfile1D(Lnormalized, param.FogDirection);
#endif
		if (density)
		{
			float scattering = GetPhysicalLightAttenuation(L, param.FogRange, param.FogAttenuationBulb);
			scattering *= density;
			scattering *= sampleOpticalDepth;
			scattering *= scatteringDepth;
#if VOLUMETRIC_FOG_ANISOTROPY == 2
			scattering *= ComputePhaseMieHG(dot(-V, Lnormalized), param.FogMiePhase) * PI;
#endif

			scatteringMie += scattering;
		}
	}

	float scattering = scatteringMie;
	scattering *= param.FogIntensity;
#if VOLUMETRIC_FOG_ANISOTROPY == 1
	scattering *= ComputePhaseMieHG(dot(-V, param.FogDirection), param.FogMiePhase);
#endif

	return float4(param.FogColor * scattering + scattering * jitter / 255.0f, scattering * param.FogIntensity);
}

void SphereFogVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float3 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{
	Position.xyz = FogPosition + Normal * FogRange;
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
	oTexcoord1 = normalize(Position.xyz - CameraPosition);
}

float4 SphereFogPS(float4 texcoord : TEXCOORD0, float3 viewdir : TEXCOORD1, in float4 screenPosition : SV_Position) : COLOR
{
	FogParams param;
	param.FogPosition = FogPosition;
	param.FogDirection = FogDirection;
	param.FogRange = FogRange;
	param.FogIntensity = FogIntensity;
	param.FogColor = FogColor;
	param.FogMiePhase = FogMiePhase;
	param.FogAttenuationBulb = FogAttenuationBulb;
	param.FogDensity = FogDensity;

	float3 V = normalize(viewdir);
	float2 intersections = ComputeRaySphereIntersection(CameraPosition, V, param.FogPosition, param.FogRange);
	clip(intersections.y);

	float linearDepth = tex2Dlod(Gbuffer8Map, float4(texcoord.xy / texcoord.w, 0, 0)).r;
	clip(linearDepth - intersections.x);

	float3 start = CameraPosition + V * max(0, intersections.x);
	float3 end = CameraPosition + V * min(linearDepth, intersections.y);

	return VolumetricFog(param, V, start, end, PseudoRandom(screenPosition.xy * (time + PI)));
}

technique MainTechBS0<string MMDPass = "object_ss"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 SphereFogVS();
		PixelShader  = compile ps_3_0 SphereFogPS();
	}
}

technique DepthTec<string MMDPass = "object";>{}
technique EdgeTec<string MMDPass = "edge";> {}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}